apply plugin: 'com.android.library'
apply from: 'maven.gradle'

android {
  compileSdkVersion 30

  sourceSets {
    androidTest.java.srcDirs += "../quack-java/src/test/java/"
  }

  dependencies {
    implementation project(':quack-java')
    testImplementation "junit:junit:${versions.junit}"
    androidTestImplementation 'com.android.support.test:runner:1.4.0'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.4.0'
  }

  compileOptions {
    sourceCompatibility 1.8
    targetCompatibility 1.8
  }

  defaultConfig {
    minSdkVersion 21
    targetSdkVersion 30
    versionCode versions.code
    versionName versions.name
    ndk {
//      abiFilters(*rootProject.ext.ndkAbiFilters)
    }
    externalNativeBuild {
      cmake {
        arguments '-DANDROID_TOOLCHAIN=clang', '-DANDROID_STL=c++_static'
        cFlags '-std=c99', '-fstrict-aliasing', '-DDUK_USE_DEBUGGER_SUPPORT', '-DDUK_USE_INTERRUPT_COUNTER', '-DDUK_USE_DEBUGGER_INSPECT', '-DDUK_USE_DEBUGGER_THROW_NOTIFY', '-DDUK_USE_DEBUGGER_PAUSE_UNCAUGHT', '-DDUK_USE_DEBUGGER_DUMPHEAP', '-Werror', '-Wno-pointer-to-int-cast'
        cppFlags '-std=c++11', '-fstrict-aliasing', '-fexceptions', '-Werror'
      }
    }

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }
  buildTypes {
    release {
      externalNativeBuild {
        cmake {
          arguments '-DCMAKE_BUILD_TYPE=MinSizeRel'
          cFlags '-g0', '-Os', '-fomit-frame-pointer', '-DNDEBUG', '-fvisibility=hidden', '-Werror'
          cppFlags '-g0', '-Os', '-fomit-frame-pointer', '-DNDEBUG', '-fvisibility=hidden', '-Werror'
        }
      }
    }
    debug {
      externalNativeBuild {
        cmake {
          cFlags '-g', '-DDEBUG', '-Werror'
          cppFlags '-g', '-DDEBUG', '-Werror'
        }
      }
    }
  }
  externalNativeBuild {
    cmake {
      path "src/main/jni/CMakeLists.txt"
    }
  }
}
